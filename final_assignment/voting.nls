;;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;;                                    VOTING
;;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

to ask-votes
  ; Ask all the known buses to vote
  let action "ASK-VOTES"
  let content 0

  foreach known-buses
  [
     send-message-with-contract ? action content (task update-votes)
  ]
end

to-report voting-finished
  ;; Check whether all the votes of the current voting have been cast
  report length known-buses + 1 = length received-votes ; Plus vote of mediator bus
end

to-report update-votes [ response ]
  ;; Add a cast vote to the "ballot box"
  let vote table:get response "content"
  let sender table:get response "sender"

  set received-votes fput vote received-votes
  report true
end

to-report cast-vote [ request ]
  ;; Cast vote on which type of bus should be added based on history of disappointments (size 3)
  let action table:get request "action"

  if action = "ASK-VOTES"
  [
     ; Cast the vote
     let vote 0
     let sender-id table:get request "sender"
     let contract-id table:get request "contract-id"

     ; Take average of the history of disappointments of size 3
     let bus-type-threshold sum disappointments / length disappointments
     ifelse bus-type-threshold > 200
     [
        set vote 3
     ]
     [
        ifelse bus-type-threshold > 50 and bus-type-threshold < 200
        [
            set vote 2
        ]
        [
            set vote 1
        ]
     ]

     let response vote

     let message create-message contract-id table:get request "contract-from" table:get request "contract-to" sender-id action response
     send-message sender-id message
     report fulfill-assigned-contract contract-id
  ]

  report false
end

to-report analyze-bus-type-votes
  ;; Analyze the votes cast and make a decision on the bus_type to order
  let total 0

  let bus-type-threshold sum disappointments / length disappointments
  ifelse bus-type-threshold > 200
  [
     set total 3
  ]
  [
     ifelse bus-type-threshold > 50 and bus-type-threshold < 200
     [
         set total 2
     ]
     [
         set total 1
     ]
  ]

  foreach received-votes
  [
    set total total + ?
  ]
  set total total / (length received-votes + 1)

  report ceiling total
end
